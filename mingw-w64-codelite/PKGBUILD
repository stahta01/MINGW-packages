# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Tim Stahlhut <stahta01@gmail.com>

# See also https://aur.archlinux.org/packages/codelite-unstable

#_ctags_git_commit="52c724d1132d78ea44894bfe2eaca44f38a9bd85"
_dbgd_git_commit="cb1c8e4655ed96879f210efc7d2f2c039698b003"

_realname=codelite
_wx_basever=3.2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-wxcrafter")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=16.4.0
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
license=('GPL')
url="https://www.codelite.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-libssh"
         #"${MINGW_PACKAGE_PREFIX}-drmingw"
         "${MINGW_PACKAGE_PREFIX}-clang"      # libclang
         "${MINGW_PACKAGE_PREFIX}-uchardet"
         "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs"
         "${MINGW_PACKAGE_PREFIX}-sqlite3")
makedepends=('git' 'openssh' 'unzip'
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-libffi"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python-pip"
             "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw")
options=(strip staticlibs !debug)

source=("${_realname}-${pkgver}.tar.gz::https://github.com/eranif/codelite/archive/${pkgver}.tar.gz"
        "codelite-dbgd.tar.gz::https://github.com/eranif/dbgd/tarball/${_dbgd_git_commit}"
        '018-Add-ws2_32-library-to-linking-of-dapcxx.patch'
        '020-wxCrafter-standalone-build-fixes-for-MSys2.patch')
sha256sums=('60ed21b27a6bff5815bf93573f04c2dc1fc8b7088539daa0f9ff242cfc966fb4'
            '909d3b1367badd29808ecedd60cfdb4884de8850366d537fc1c54e9e8662cd39'
            'f51c4c75110a4f1bb46ff0f47964847111c123abc52dc604bd952116c02f6a3d'
            'c83b521cf2569c32647666b125ffe66a40557dfa8ffb3c9a3336e616655f7ce6')
prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  rm -rf wxdap
  git clone https://github.com/eranif/dbgd.git wxdap
  #rm -rf ctags
  #git clone https://github.com/eranif/ctags.git

  # wxcrafter plugin is seg faulting; but stand-alone wxcrafter works.
  patch -p1 -i "${srcdir}"/020-wxCrafter-standalone-build-fixes-for-MSys2.patch

  pushd wxdap
  patch -p1 -i "${srcdir}"/018-Add-ws2_32-library-to-linking-of-dapcxx.patch
  popd
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  extra_config+=( -Wno-dev ) # Hides developer warnings

  # disables the part of urlmon.h that defines IsLoggingEnabled
  # This replaces 010-strange-conflicts.patch
  CXXFLAGS+=" -D_HITLOGGING_DEFINED=0"

  # build wxCrafter as stand alone app
  mkdir -p build-${MSYSTEM}-wxcrafter
  cd build-${MSYSTEM}-wxcrafter

  CC="clang" CXX="clang++" \
  MSYS2_ARG_CONV_EXCL="-DCL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCL_PREFIX=${MINGW_PREFIX} \
    -DWXC_APP=1 \
    -DENABLE_SFTP=0 \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-${_wx_basever} \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  make VERBOSE=1 # -j1

  rm -fr ${srcdir}/${MINGW_PREFIX}
  make DESTDIR=${srcdir} install

  cd "${srcdir}/${_realname}-${pkgver}"
  # Remove folder to prevent wxcrafter plugin from being build
  rm -rf wxcrafter

  cd "${srcdir}"

  mkdir -p build-${MSYSTEM}  
  cd build-${MSYSTEM}  

  CC="clang" CXX="clang++" \
  MSYS2_ARG_CONV_EXCL="-DCL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCL_PREFIX=${MINGW_PREFIX} \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-${_wx_basever} \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  make VERBOSE=1 # -j1 
}

package_codelite() {
  optdepends=("${MINGW_PACKAGE_PREFIX}-clang"
              "${MINGW_PACKAGE_PREFIX}-${_realname}-wxcrafter")

  cd build-${MSYSTEM}  
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  # why?
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/codelite/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.a
}

package_codelite-wxcrafter() {
  pkgdesc="CodeLite's wxCrafter (mingw-w64)"
  optdepends=()
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cp --recursive ${srcdir}/${MINGW_PREFIX} ${pkgdir}
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-wxcrafter/LICENSE

  # Remove DLLs provided by CodeLite IDE
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/codelite.dll
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libplugin.dll
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libwxshapeframework.dll
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libwxsqlite3.dll
  # Remove lib that makes sense only for IDE package
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
