####
#
# Based on packages found at these URLs
#     https://archlinux.org/packages/extra/x86_64/wxgtk-common/
#     https://github.com/msys2/MINGW-packages/tree/master/mingw-w64-wxWidgets
#
# Maintainer: Tim Stahlhut <stahta01@gmail.com>
#
####

_basename=wxWidgets
_sourcedir=${_basename}-git
_wx_basever=3.3
_namesuffix="-git"
pkgbase=mingw-w64-${_basename}${_namesuffix}
pkgname=(
  ${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common${_namesuffix}
  ${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs${_namesuffix}
  ${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-cb_headers${_namesuffix}
  ${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs${_namesuffix}
  ${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw${_namesuffix}
  #$([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-gtk3-libs${_namesuffix}")
  #$([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-gtk3${_namesuffix}")
  #$([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-qt-libs${_namesuffix}")
  #$([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-qt${_namesuffix}")
)
pkgver=3.3.2.0.v+855.c2.gde25991f88c
pkgrel=1
pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
license=("custom:wxWindows")
url="https://www.wxwidgets.org/"
makedepends=(
  'diffutils'
  'git'
  'make'
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-libsecret")
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-libnotify")
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-gtk3")
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-gspell")
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-pkgconf")
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-qt6-base")
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
  "${MINGW_PACKAGE_PREFIX}-libtiff"

  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-python"
)
checkdepends=()
#options=('strip' 'staticlibs' 'buildflags' '!debug')
options=('!strip' 'staticlibs' 'buildflags' '!debug')
#options=('!strip' 'staticlibs' 'buildflags' 'debug')
source=(
  #${_sourcedir}::"git+https://github.com/wxWidgets/wxWidgets.git#commit=b6e88ef422e333e32a52e08d2b159bb51035989d" # Still had image load bug
  ${_sourcedir}::"git+https://github.com/wxWidgets/wxWidgets.git#branch=master"

  001-wxWidgets-3.0.2-relocate-prefix-in-bin-wx-config.patch

  004-use-copy-instead-of-symlink-on-windows.patch

  # Patch rejected by wxWidgets team
  005-wxWidgets-3.1.3-Remove-WX_LIBS_STATIC-from-m4.patch

  201-Fix-warning-in-trackball-sample.patch
)
sha256sums=('SKIP'
            '2b3b183a6a76ad539abc49a41033aa923c13b395c0e55189ba962068781c7135'
            '99012dfafda3ef9fcd14a123e1bc9c23818d9d82e64d774d1e40752b75413447'
            '4a4828f0c9cdc638cffde6a30b5dfb14283719acc9e89e19de8ec2d5a80a5aec'
            'e322bd18d8fd254e090816ceab0bd53d05660ab57684400e9ae32d6fc8835693')

# Declare global variables; begin with underscore to avoid name conflicts
_git_base_commit=
_build_suffix=${MSYSTEM}

pkgver() {
  cd ${srcdir}/${_sourcedir}
  local _major=$(head -n 34 include/wx/version.h | grep 'wxMAJOR_VERSION' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor1=$(head -n 34 include/wx/version.h | grep 'wxMINOR_VERSION' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor2=$(head -n 34 include/wx/version.h | grep 'wxRELEASE_NUMBER' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor3=$(head -n 34 include/wx/version.h | grep 'wxSUBRELEASE_NUMBER' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  printf "%s.%s.%s.%s.v+%s.c%s.g%s" "$_major" "$_minor1" "$_minor2" "$_minor3" $(git rev-list --count $(git rev-list -1 ${_git_base_commit} include/wx/version.h)..${_git_base_commit}) $(git rev-list --count ${_git_base_commit}..HEAD) $(git rev-parse --short=11 ${_git_base_commit})
}

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_sourcedir}"

  git submodule init
  git submodule update

  _git_base_commit=$(git rev-parse HEAD)
  GIT_AM="git am --committer-date-is-author-date"

  git config --local user.name  "nobody"
  git config --local user.email "nobody@example.com"

  # Fix Run-Time wx-config bug: MSys2 Mingw only patch; Do not try to submit upstream
  patch -p1 -i "${srcdir}"/001-wxWidgets-3.0.2-relocate-prefix-in-bin-wx-config.patch

  #apply_patch_with_msg \
    #004-use-copy-instead-of-symlink-on-windows.patch

  #git am --abort || true

  # MSys2 fails to set WX_LIBS_STATIC correctly; removed to see if something breaks.
  ${GIT_AM} ${srcdir}/005-wxWidgets-3.1.3-Remove-WX_LIBS_STATIC-from-m4.patch

  ${GIT_AM} ${srcdir}/201-Fix-warning-in-trackball-sample.patch
}

build() {
  ####
  # Configure options added to support other software:
  #   -DwxUSE_GRAPHICS_CONTEXT=ON   codelite
  #   -DwxUSE_GRAPHICS_DIRECT2D     codeblocks
  #
  # Configure options added to check for build issues
  #   -DwxBUILD_PRECOMP=OFF
  #
  # Configure options added to avoid possible future issues
  #   -DCMAKE_CXX_STANDARD=17
  #   -DwxUSE_STL=ON
  #
  # Configure options added to avoid warnings:
  #   -DwxUSE_REGEX=builtin
  #
  # Configure options known to cause build errors:
  #   -DwxUSE_REGKEY=OFF                                compile error
  #
  # Configure options known to cause build errors under wxGTK/Win:
  #   -DwxUSE_OPENGL=ON                                 configure error
  #   -DwxUSE_ACCESSIBILITY=ON                          compile error
  #     "wxUSE_ACCESSIBILITY is currently only supported under wxMSW"
  #
  # Configure options added to avoid wxGTK/Win build errors:
  #   -DwxUSE_OPENGL=OFF
  #
  ####

  declare -a _common_config
  _common_config+=(
    -G"Ninja"
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}
    -DCMAKE_CXX_STANDARD=17
    -DwxBUILD_INSTALL_PLATFORM_SUBDIR=OFF
    -DwxBUILD_INSTALL=ON
    -DwxBUILD_WIN32_MSVC_NAMING=OFF
    -DwxUSE_IFF=ON
    -DwxUSE_GRAPHICS_CONTEXT=ON
    -DwxUSE_GRAPHICS_DIRECT2D=ON
    -DwxBUILD_MONOLITHIC=OFF
    -DwxBUILD_PRECOMP=OFF
    -DwxBUILD_TESTS=OFF
    -DwxUSE_LIBLZMA=ON
    -DwxUSE_WEBREQUEST_WINHTTP=ON
    -DwxUSE_STACKWALKER=ON
    -DwxUSE_DBGHELP=ON
  )

  if check_option "debug" "y"; then
    _common_config+=("-DCMAKE_BUILD_TYPE=Debug")
  else
    _common_config+=("-DCMAKE_BUILD_TYPE=Release" "-DwxBUILD_OPTIMISE=ON")
  fi

  CXXFLAGS+=" -Wno-ignored-attributes -Wno-enum-compare-switch -Wno-deprecated-declarations"

  if [[ "$MSYSTEM_CARCH" == "i686" ]]; then
    _toolkits="msw"
  else
    _toolkits="msw gtk3 qt"
  fi

  for _toolkit in ${_toolkits}; do
    for _libtype in shared static; do

      if [[ ${_toolkit} == qt && ${_libtype} == static ]]; then
        continue
      fi
      if [[ ${_toolkit} == gtk3 ]]; then
        _opengl="OFF"
      else
        _opengl="ON"
      fi
      if [[ ${_libtype} == shared ]]; then
        _is_shared="ON"
        _is_external="sys"
      else
        _is_shared="OFF"
        _is_external="builtin"
      fi

      MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        cmake \
          -DBUILD_SHARED_LIBS=${_is_shared} \
          -DwxBUILD_SHARED=${_is_shared} \
          -DwxUSE_WEBREQUEST_CURL=${_is_shared} \
          -DwxBUILD_TOOLKIT=${_toolkit} \
          -DwxUSE_EXPAT=${_is_external} \
          -DwxUSE_LIBJPEG=${_is_external} \
          -DwxUSE_LIBPNG=${_is_external} \
          -DwxUSE_LIBTIFF=${_is_external} \
          -DwxUSE_LIBWEBP=${_is_external} \
          -DwxUSE_REGEX=${_is_external} \
          -DwxUSE_ZLIB=${_is_external} \
          -DwxUSE_OPENGL=${_opengl} \
          "${_common_config[@]}" \
          -S wxWidgets-git \
          -B build-${_toolkit}-${MSYSTEM}-${_libtype}

      cmake --build build-${_toolkit}-${MSYSTEM}-${_libtype}

      DESTDIR="${srcdir}"/install-${_toolkit}-${MSYSTEM}-${_libtype} \
      cmake --install build-${_toolkit}-${MSYSTEM}-${_libtype}

    done
  done
}

check() {
  cd "${srcdir}/build-msw-${MSYSTEM}-shared/samples"    && make -k --jobs=1 || true
#  cd "${srcdir}/build-msw-${MSYSTEM}-shared/tests"      && make -k --jobs=1 || true

#  cd "${srcdir}"/build-gtk3-${MSYSTEM}-shared/samples   && make -k --jobs=1 # || true
#  cd "${srcdir}"/build-gtk3-${MSYSTEM}-shared/tests     && make -k --jobs=1 # || true
}

package_wxwidgets3.3-common-git() {
  pkgdesc="Static libraries and headers for wxWidgets ${_wx_basever} (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs"
           "${MINGW_PACKAGE_PREFIX}-cc-libs")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets-common" "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets-common" "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share
  cp -a --recursive "${srcdir}/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/share/" \
      "${pkgdir}"${MINGW_PREFIX}/

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/include
  cp -a --recursive "${srcdir}/install-qt-${MSYSTEM}-shared${MINGW_PREFIX}/include/" \
      "${pkgdir}"${MINGW_PREFIX}/

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/include
  cp -a --recursive "${srcdir}/install-gtk3-${MSYSTEM}-shared${MINGW_PREFIX}/include/" \
      "${pkgdir}"${MINGW_PREFIX}/

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/include
  cp -a --recursive "${srcdir}/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/include/" \
      "${pkgdir}"${MINGW_PREFIX}/

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/bin/wxrc*.* "${pkgdir}"${MINGW_PREFIX}/bin/

  install -Dm644 wxWidgets-git/wxwin.m4 \
    -t "${pkgdir}"${MINGW_PREFIX}/share/aclocal

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/lib
  cp -a "${srcdir}"/install-qt-${MSYSTEM}-shared${MINGW_PREFIX}/lib/*.a  "${pkgdir}"${MINGW_PREFIX}/lib/
  cp -a "${srcdir}"/install-gtk3-${MSYSTEM}-static${MINGW_PREFIX}/lib/*.a "${pkgdir}"${MINGW_PREFIX}/lib/
  cp -a "${srcdir}"/install-gtk3-${MSYSTEM}-shared${MINGW_PREFIX}/lib/*.a "${pkgdir}"${MINGW_PREFIX}/lib/
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-static${MINGW_PREFIX}/lib/*.a  "${pkgdir}"${MINGW_PREFIX}/lib/
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/lib/*.a  "${pkgdir}"${MINGW_PREFIX}/lib/
}

package_wxwidgets3.3-msw-cb_headers-git() {
  pkgdesc="private wxWidgets ${_wx_basever} MSW headers needed by Code::Blocks (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets-msw-cb_headers")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets-msw-cb_headers")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/include/wx-${_wx_basever}/wx/msw/private
  cd "${pkgdir}"${MINGW_PREFIX}/include/wx-${_wx_basever}/wx/msw/private
  cp "${srcdir}"/wxWidgets-git/include/wx/msw/private/comptr.h ./
  cp "${srcdir}"/wxWidgets-git/include/wx/msw/private/graphicsd2d.h ./
  cp "${srcdir}"/wxWidgets-git/include/wx/msw/private/keyboard.h ./
}

package_wxwidgets3.3-common-libs-git() {
  pkgdesc="wxBase shared libraries for wxwidgets ${_wx_basever} (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-curl"
           "${MINGW_PACKAGE_PREFIX}-expat"
           "${MINGW_PACKAGE_PREFIX}-cc-libs"
           "${MINGW_PACKAGE_PREFIX}-pcre2"
           "${MINGW_PACKAGE_PREFIX}-xz"
           "${MINGW_PACKAGE_PREFIX}-zlib")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/bin/*.dll "${pkgdir}"${MINGW_PREFIX}/bin/

  rm    "$pkgdir"${MINGW_PREFIX}/bin/wxmsw*.dll   # in msw libs package

  # License files
  cd "${srcdir}"/wxWidgets-git/docs
  install -Dm644 preamble.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/preamble.txt"
  install -Dm644 licence.txt  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/licence.txt"
  install -Dm644 licendoc.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/licendoc.txt"
  install -Dm644 lgpl.txt     "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/lgpl.txt"
  install -Dm644 gpl.txt      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/gpl.txt"
  install -Dm644 xserver.txt  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/xserver.txt"
}

package_wxwidgets3.3-msw-libs-git() {
  pkgdesc="wxMSW shared libraries for wxwidgets ${_wx_basever} (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
           "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
           "${MINGW_PACKAGE_PREFIX}-libpng"
           "${MINGW_PACKAGE_PREFIX}-libtiff"
           "${MINGW_PACKAGE_PREFIX}-libwebp"
           "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/bin/*.dll "${pkgdir}"${MINGW_PREFIX}/bin/

  rm    "$pkgdir"${MINGW_PREFIX}/bin/wxbase*.dll  # in base libs package
}

package_wxwidgets3.3-msw-git() {
  pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs"
           "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}"
            "${MINGW_PACKAGE_PREFIX}-wxwidgets-msw")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets-msw")


  mkdir -p "${pkgdir}"${MINGW_PREFIX}/lib
  cp -a --recursive "${srcdir}/install-msw-${MSYSTEM}-static${MINGW_PREFIX}/lib/wx/" \
      "${pkgdir}"${MINGW_PREFIX}/lib

  cp -a --recursive "${srcdir}/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/lib/wx/" \
      "${pkgdir}"${MINGW_PREFIX}/lib

  cp -a --recursive "${srcdir}"/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/lib/cmake/ \
      "${pkgdir}"${MINGW_PREFIX}/lib

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-static${MINGW_PREFIX}/bin/wx-config* "${pkgdir}"${MINGW_PREFIX}/bin/
  mv "${pkgdir}"${MINGW_PREFIX}/bin/wx-config{,-static}
  cp -a "${srcdir}"/install-msw-${MSYSTEM}-shared${MINGW_PREFIX}/bin/wx-config* "${pkgdir}"${MINGW_PREFIX}/bin/

  # Replace real full path with psuedo full path; to prevent
  # WARNING: Package contains reference to $(cygpath -m /)
  local MINGW_PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config"
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config-static"
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-${_wx_basever}"
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-static-${_wx_basever}"

  # Workaround for https://github.com/wxWidgets/wxWidgets/issues/23314
  if [[ ${MSYSTEM} == CLANG* ]]; then
    sed -s "s|\"-mthreads\"|\"\"|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config"
    sed -s "s|\"-mthreads\"|\"\"|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config-static"
  fi

  # CLANG gives warnings and in some cases errors on "-mwindows" option while building wxPython
  if [[ ${MSYSTEM} == CLANG* ]]; then
    sed -s "s| -mwindows||g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config"
    sed -s "s| -mwindows||g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config-static"
    sed -s "s| -mwindows||g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-${_wx_basever}"
    sed -s "s| -mwindows||g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-static-${_wx_basever}"
  fi

  # create wx-config copy with version file suffix
  cp -a "${pkgdir}"${MINGW_PREFIX}/bin/wx-config{,-${_wx_basever}}
}

package_wxwidgets3.3-gtk3-libs-git() {
  pkgdesc="GTK+3 implementated shared libraries for wxwidgets ${_wx_basever} (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-cairo"
           "${MINGW_PACKAGE_PREFIX}-cc-libs"
           "${MINGW_PACKAGE_PREFIX}-fontconfig"
           "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
           "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
           "${MINGW_PACKAGE_PREFIX}-glib2"
           "${MINGW_PACKAGE_PREFIX}-gspell"
           "${MINGW_PACKAGE_PREFIX}-gtk3"
           "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
           "${MINGW_PACKAGE_PREFIX}-libpng"
           "${MINGW_PACKAGE_PREFIX}-libtiff"
           "${MINGW_PACKAGE_PREFIX}-libwebp"
           "${MINGW_PACKAGE_PREFIX}-pango"
           "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-gtk3-${MSYSTEM}-shared${MINGW_PREFIX}/bin/*.dll "${pkgdir}"${MINGW_PREFIX}/bin/

  rm    "$pkgdir"${MINGW_PREFIX}/bin/wxbase*.dll  # in base libs package
}

package_wxwidgets3.3-gtk3-git() {
  pkgdesc="GTK+3 implementation of wxWidgets ${_wx_basever} C++ GUI API (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-gtk3-libs"
           "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets-gtk3")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets-gtk3")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/lib
  cp -a --recursive "${srcdir}/install-gtk3-${MSYSTEM}-static${MINGW_PREFIX}/lib/wx/" \
      "${pkgdir}"${MINGW_PREFIX}/lib

  cp -a --recursive "${srcdir}/install-gtk3-${MSYSTEM}-shared${MINGW_PREFIX}/lib/wx/" \
      "${pkgdir}"${MINGW_PREFIX}/lib

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-gtk3-${MSYSTEM}-static${MINGW_PREFIX}/bin/wx-config* "${pkgdir}"${MINGW_PREFIX}/bin/
  cp -a "${srcdir}"/install-gtk3-${MSYSTEM}-shared${MINGW_PREFIX}/bin/wx-config* "${pkgdir}"${MINGW_PREFIX}/bin/

  mv "$pkgdir"${MINGW_PREFIX}/bin/wx-config{,-gtk3} # Conflicts with msw

  # Replace real full path with psuedo full path; to prevent
  # WARNING: Package contains reference to $(cygpath -m /)
  local MINGW_PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config-gtk3"
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/gtk3-unicode-${_wx_basever}"
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/gtk3-unicode-static-${_wx_basever}"

  # create wx-config-gtk3 copy with version file suffix
  cp -a "${pkgdir}"${MINGW_PREFIX}/bin/wx-config-gtk3{,-${_wx_basever}}
}

package_wxwidgets3.3-qt-libs-git() {
  pkgdesc="Qt implementated shared libraries for wxwidgets ${_wx_basever} (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
           "${MINGW_PACKAGE_PREFIX}-qt6-base"
           "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
           "${MINGW_PACKAGE_PREFIX}-libpng"
           "${MINGW_PACKAGE_PREFIX}-libtiff"
           "${MINGW_PACKAGE_PREFIX}-libwebp"
           "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common-libs")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-qt-${MSYSTEM}-shared${MINGW_PREFIX}/bin/*.dll "${pkgdir}"${MINGW_PREFIX}/bin/

  rm    "$pkgdir"${MINGW_PREFIX}/bin/wxbase*.dll  # in base libs package
}

package_wxwidgets3.3-qt-git() {
  pkgdesc="Qt implementation of wxWidgets ${_wx_basever} C++ GUI API (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-qt-libs"
           "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-common")
  provides=("${MINGW_PACKAGE_PREFIX}-wxwidgets-qt")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxwidgets-qt")

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/lib
  cp -a --recursive "${srcdir}"/install-qt-${MSYSTEM}-shared${MINGW_PREFIX}/lib/wx/ \
      "${pkgdir}"${MINGW_PREFIX}/lib

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp -a "${srcdir}"/install-qt-${MSYSTEM}-shared${MINGW_PREFIX}/bin/wx-config* "${pkgdir}"${MINGW_PREFIX}/bin/

  mv "$pkgdir"${MINGW_PREFIX}/bin/wx-config{,-qt} # Conflicts with msw

  # Replace real full path with psuedo full path; to prevent
  # WARNING: Package contains reference to $(cygpath -m /)
  local MINGW_PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config-qt"
  sed -s "s|${MINGW_PREFIX_WIN}|\$\{prefix\}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/qt-unicode-${_wx_basever}"

  # create wx-config-qt copy with version file suffix
  cp -a "${pkgdir}"${MINGW_PREFIX}/bin/wx-config-qt{,-${_wx_basever}}
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
